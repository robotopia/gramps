# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>

pkgname=gramps
pkgver=5.1.5
pkgrel=1
epoch=2
pkgdesc="Genealogy program, which helps you track your family tree"
arch=('any')
url="https://gramps-project.org/"
license=('GPL')
depends=('gtk3' 'python-bsddb' 'python-cairo' 'python-gobject' 'xdg-utils')
makedepends=('intltool')
optdepends=('osm-gps-map: show maps in the geography view'
            'graphviz: enable creation of graphs (required for tree reports)'
            'python-pyicu: improves localised sorting'
            'gtkspell3: enable spell checking in the notes'
            'rcs: manage multiple revisions of your family trees'
            'python-pillow: crop and convert images to JPG'
            'libgexiv2: manage Exif metadata embedded in your media'
            'geocode-glib: associate a geographical position to a place name')
source=("git+https://github.com/robotopia/$pkgname.git#branch=clireport")
md5sums=('SKIP')

build() {
  cd $pkgname
  python3 setup.py build
}

package() {
  cd $pkgname
  python3 setup.py install --no-compress-manpages --root="$pkgdir" --optimize=1
  echo -n '/usr/share' >"$pkgdir"/usr/lib/python3.10/site-packages/gramps/gen/utils/resource-path
}
